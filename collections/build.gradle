plugins {
    id("java")

    // See individual plugins
    // https://micronaut-projects.github.io/micronaut-gradle-plugin/latest/
    id("io.micronaut.minimal.application") version "3.2.2"
    id("com.github.johnrengelman.shadow") version "7.1.1"
    id("jacoco")
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'io.micronaut.minimal.application'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    java {
        sourceCompatibility = JavaVersion.toVersion("11")
        targetCompatibility = JavaVersion.toVersion("11")
    }

    dependencies {
        annotationProcessor("org.projectlombok:lombok:$lombokVersion")
        implementation "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")
    }

    //TODO: only in data-indexer

    // From micronaut 3.x this is required
    // also disabling Native Image as it is
    // not used in 1st gen Cloud functions
    micronaut {
        runtime("google_function")
        testRuntime("junit5")
        enableNativeImage(false)
        processing {
            incremental(true)
        }
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }

        // Uncomment these lines to get full logging output when running
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat = 'full'

        finalizedBy jacocoTestReport // report is always generated after tests run
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
    }

}

apply plugin: 'idea'

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
        excludeDirs = [file('.gradle'), file(".idea"), file('build')]
    }
}
