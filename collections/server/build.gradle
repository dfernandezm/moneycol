plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "application"
    id 'com.google.cloud.tools.jib' version '2.6.0'
}

version "0.1.0"
group "server"

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly
}

ext {
    logbackVersion = "1.2.3"
    lombokVersion = "1.18.8"
    junitVersion = "5.6.0"
    mockitoJunitJupiterVersion = "2.22.0"
    firebaseSdkVersion = "6.12.2"
    micronautJunit5Version = "1.1.5"
    hamcrestVersion = "1.3"
    micronautVersion = "2.0.3"
}

dependencies {
    annotationProcessor(platform("io.micronaut:micronaut-bom:$micronautVersion"))
    annotationProcessor("io.micronaut:micronaut-inject-java")
    annotationProcessor("io.micronaut:micronaut-validation")
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")
    implementation(platform("io.micronaut:micronaut-bom:$micronautVersion"))
    implementation("io.micronaut:micronaut-inject")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut:micronaut-http-server-netty")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut.security:micronaut-security-jwt")
    developmentOnly("io.micronaut:micronaut-runtime-osx:$micronautVersion")
    implementation "org.projectlombok:lombok:$lombokVersion"

    implementation ("com.google.firebase:firebase-admin:$firebaseSdkVersion") {
        exclude group: "com.google.cloud", module: "google-cloud-firestore"
    }

    implementation group: 'com.google.cloud', name: 'google-cloud-firestore', version: '2.1.0'

    runtimeOnly("ch.qos.logback:logback-classic")
    testAnnotationProcessor(enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion"))
    testAnnotationProcessor("io.micronaut:micronaut-inject-java")
    testImplementation(enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion"))
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("io.micronaut.test:micronaut-test-junit5")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    
    testImplementation "io.micronaut.test:micronaut-test-junit5:$micronautJunit5Version"
    testImplementation "io.micronaut.test:micronaut-test-core:$micronautJunit5Version"
    testImplementation "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoJunitJupiterVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"

    testImplementation "org.testcontainers:testcontainers:1.15.0"
    testImplementation "org.testcontainers:junit-jupiter:1.15.0"
    testImplementation "org.testcontainers:gcloud:1.15.0"
    testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'

    testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")
}

jib.to.image = "eu.gcr.io/moneycol/collections-api:0.1.0"
jib.from.image = 'gcr.io/distroless/java:11-debug'

test.classpath += configurations.developmentOnly

mainClassName = "com.moneycol.collections.server.Application"

// use JUnit 5 platform
test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
}

shadowJar {
    mergeServiceFiles()
}

run.classpath += configurations.developmentOnly
run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
